options
{
  LOOKAHEAD=1;
}

PARSER_BEGIN(Parser)

import java.io.*;
import java.util.ArrayList;

public class Parser
{
	public static void main(String args[]) throws ParseException, IOException {
		if(args.length != 1){
			System.out.println("Usage: Parser <file.yal>");
			return;
		}
		String filename = args[0];
		try{

			BufferedReader br = new BufferedReader(new FileReader(filename));
			Parser simpleParser = new Parser(br);
			SimpleNode root = simpleParser.Module();
			semantic.HIRTree hir = new semantic.HIRTree();
			semantic.Table rootTable = new semantic.Table();
	 			//TODO for cycle to add all files pass in args
 			semantic.SemanticChecker smc=  new semantic.SemanticChecker(rootTable);
 			root.dump("", hir);
	  		hir.dump("");
	  		smc.runSemanticCheck(hir, smc.getTable());

	  		  ArrayList<semantic.Error>erros = smc.getError_list();
                        
                        for (int i=0;i<erros.size();i++){
                        	erros.get(i).printError();
                        }
            if(erros.size() == 0) { 
	  		codeGenerator.CodeGenerator cg = new codeGenerator.CodeGenerator(hir, smc.getTable());
	  		cg.generateCode();
	  	}
	  	}catch(IOException e){
	  		e.printStackTrace();
	  	}
	}
}

PARSER_END(Parser)

SKIP:
{
	" "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
| <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

/* reserved words */
TOKEN : {
<RELA_OP: ">" | "<" | "<=" | ">=" | "==" | "!=">
| <ADDSUB_OP: "+" | "-">
| <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>">
| <BITWISE_OP: "&" | "|" | "^">
| <NOT_OP: "!">
| <WHILE: "while">
| <IF: "if">
| <ELSE: "else">
| <ASSIGN: "=">
| <ASPA: "\"">
| <LPAR: "(">
| <RPAR: ")">
| <VIRG: ",">
| <PVIRG: ";">
| <LCHAVETA: "{">
| <RCHAVETA: "}">
| <FUNCTION: "function">
| <MODULE: "module">
| <SIZE: "size">
| < DOTSIZE: ".size" >
}

TOKEN : {
<INTEGER: (<DIGIT>)+>
| <ID: <LETTER> (<LETTER> | <DIGIT>)*>
| <#LETTER: ["$","A"-"Z","_","a"-"z"]>
| <#DIGIT: ["0"-"9"]>
| <STRING: "\"" (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ "\"">
}

SimpleNode Module() : { }
{
	<MODULE>Id()<LCHAVETA>(DeclarationList())(Function())*<RCHAVETA> { return jjtThis; }
}

void DeclarationList(): { }
{
	(Declaration())*
}
void Declaration() : { Token assign;  }
{
	Lhs()#Array(>1)( assign=<ASSIGN> { jjtThis.val = assign.image; } DeclarationRHS() )? <PVIRG>
}

void DeclarationRHS() : { Token id = new Token(); id.image = "undefined"; }
{
	( ( "[" ArraySize() "]" ) | (id=<ADDSUB_OP> { jjtThis.val = id.image; } )? Integer() )
}


void Function() : { }
{
	  <FUNCTION> Function2()#Return(>1) <LPAR> ( Varlist()#Parameters )? <RPAR>  <LCHAVETA> Stmtlst() <RCHAVETA>
}

void Function2() #void : { Token id; }
{
	 Id() ((ArrayElement())?<ASSIGN>  Id())?
}

void Function3() #void : { }
{
  (ArrayElement())?<ASSIGN>  Id()
}

void Id() : { Token id; }
{
 id=< ID > { jjtThis.val = id.image; }
}
void Integer() : { Token integer; }
{
	integer=< INTEGER > { jjtThis.val = integer.image;}
}

void Varlist()#void: { }
{
	(  ScalarElement()#param ) ( <VIRG> ( ScalarElement()#param ) )*
}

void ScalarElement()#void: { }
{
	Id() (ArrayElement())?
}
void ArrayElement(): { }
{
	"[""]"
}
void Stmtlst() #void : { }
{
	(Stmt())*
}
void Stmt()#void: { }
{
	While() | If()(Else())? | (LOOKAHEAD(Call()) Call() < PVIRG > | Assign())   //ID
}
void Call(): {}
{
	 Id() ( ( ( "." Id() )? <LPAR> ( ArgumentList() )? <RPAR> ) | SizeAccess() )
}

void SizeAccess(): { }
{
	".size" { jjtThis.val = null;}
}

void Assign(): { Token assign; }
{
	Lhs()#Array(>1) assign=< ASSIGN > { jjtThis.val = assign.image; }(LOOKAHEAD(3) Rhs() | Call())<PVIRG>
}
void Lhs() #void : { }
{
	Id() (ArrayAccess() | ScalarAccess())
}
void Rhs() : { Token Op  = new Token(); Op.image = "Undefined";}
{
	( Term() ( ( Op=<ARITH_OP> | Op=<BITWISE_OP> | Op=<ADDSUB_OP> ) { jjtThis.val = Op.image; } Term() )? )| "[" ArraySize() "]"
}

void ArrayAccess():{ Token ind = new Token(); ind.image = "undefined"; } // -- ID
{
	"[" (ind=< ID >| ind=< INTEGER >)? "]" { jjtThis.val = ind.image=="undefined"?"null":ind.image; } //Optional Index. Now accepts statement b[] = 2; 
}
void ScalarAccess()#void:{} // -- ID
{
	 ("." <SIZE>)? 
}

void ArraySize(): { } // ++ ID
{
	(Id() ScalarAccess()) | Integer()
}
void Term(): { }
{
	( <ADDSUB_OP> )? ( Integer() | ( LOOKAHEAD(Call()) Call() | (Id() ( ArrayAccess() | ScalarAccess())) ) )  // ID
}

void Exprtest(): { Token RelOp; }
{
	<LPAR> Lhs() RelOp=<RELA_OP> { jjtThis.val = RelOp.image; } Rhs() <RPAR>
}
void While(): { }
{
	<WHILE> Exprtest() <LCHAVETA> Stmtlst() <RCHAVETA>
}
void If(): {}
{
	<IF> Exprtest() <LCHAVETA> Stmtlst() <RCHAVETA>
}
void Else(): { }
{
	<ELSE> <LCHAVETA> Stmtlst() <RCHAVETA>
}
void ArgumentList():{}
{
	Argument() ( <VIRG> Argument() )*
}
void Argument()#void:{}
{
	( Id() | String() | Integer() )
}

void String() : { Token id; }
{
 id=<STRING> { jjtThis.val = id.image; }
}

Token Index():{Token t;}
{
	(t=< ID >| t=< INTEGER >) { return t; }
}
